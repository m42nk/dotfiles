#!/usr/bin/env expect

exec terminal-notifier -title "sshuttle" -message "Connecting.." -group "loading" -sound "default"

# Read password from the user
send_user "Password: "

# Turn off echo to hide password
stty -echo

expect -re "(.*)\n"
send_user "\n"
set password $expect_out(1,string)

# Turn on echo
stty echo

spawn sshuttle -r proxy-bastion --dns 10.69.0.0/16 10.224.0.0/16

expect "\\\[local sudo\\\] Password: "
send -- "$password\r"

set code_line_number ""
expect {
  "Please choose from the available authentication methods" {
    expect {
      -re {(\d+): Security code from Google Authenticator application} {
        set code_line_number $expect_out(1,string)
        exp_continue
      }
      timeout {
        puts "\nTimeout while waiting for authentication method list"
        exit 1
      }
      eof {
        puts "\nUnexpected end of input while selecting authentication method"
        exit 1
      }
    }
    exp_continue
  }
  "Enter the number for the authentication method to use" {
    send -- "$code_line_number\r"
    exp_continue
  }
  "Enter your one-time password" {
    set otp_seed [exec security find-generic-password -a gojek-mfa -w]
    set otp [exec /opt/homebrew/bin/oathtool --totp --base32 "$otp_seed"]
    send -- "$otp\r"
    exp_continue
  }
}

expect -re "c : Connected to server."
exec terminal-notifier -title "sshuttle" -message "Connected!" -group "loading" -sound "default"

interact
