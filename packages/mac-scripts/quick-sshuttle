#!/usr/bin/env expect

set otp_seed [exec security find-generic-password -a gojek-mfa -w]

proc notify {message} {
  exec terminal-notifier -title "sshuttle" -message $message -group "sshuttle" -sound "default"
}

proc log {message} {
  send_user "\n\[log\] $message"
}

notify "Connecting..."
exec say Enter password

# Read password from the user
send_user "Password: "

# Turn off echo to hide password
stty -echo

expect -re "(.*)\n"
send_user "\n"
set password $expect_out(1,string)

# Turn on echo
stty echo

spawn sshuttle -r proxy-bastion --dns 10.69.0.0/16 10.224.0.0/16

expect "\\\[local sudo\\\] Password: "
send -- "$password\r"

log "Waiting for authentication method list"
set code_line_number ""
expect {
  "Please choose from the available authentication methods" {
    expect {
      -re {(\d+): Security code from Google Authenticator application} {
        set code_line_number $expect_out(1,string)
      }
      timeout {
        log "Timeout while waiting for authentication method list"
        interact
      }
      eof {
        log "Unexpected end of input while selecting authentication method"
        exit 1
      }
    }

    exp_continue
  }
  "Enter the number for the authentication method to use" {
    send -- "$code_line_number\r"
    exp_continue
  }
  "Enter your one-time password" {
    set otp [exec /opt/homebrew/bin/oathtool --totp --base32 "$otp_seed"]
    send -- "$otp\r"
  }
}

expect {
  -re "c : Connected to server\." {
    notify "Connected!"
  }
  timeout {
    log "Timeout while waiting for connection"
  }
}

interact
